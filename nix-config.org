#+title: Nix-Darwin-Config +
#+subtitle: Nix-powered declarative macOS configuration
#+author: Shaurya Singh
#+options: broken-links:t
#+latex_header: \let\textls\relax
#+latex_class: chameleon
#+startup: preview
#+startup: fold
#+options: broken-links:t
#+options: toc:5

#+begin_export HTML
<a href="https://github.com/shaunsingh/nix-darwin-dotfiles/"
   style="font-family: 'Open Sans'; background-image: none; color: inherit;
   text-decoration: none; position: relative; top: clamp(-26px, calc(1280px - 100vw), 0px); opacity: 0.7;">
  <img src="https://upload.wikimedia.org/wikipedia/commons/9/91/Octicons-mark-github.svg"
       class="invertible" alt="GitHub Octicon"
       style="height: 1em; position: relative; top: 0.1em;">
  View on GitHub</a>
#+end_export

* Introduction
Once upon a time I was a wee little lad playing around with vim. After that, my "ricing" addiction grew, and soon it turned into a dotfiles repo. Since I moved machines often, I wanted a simple way to install all dependencies for my system. What started off as a simple =install.sh= script turned into a dotfiles repo managed via [[https://yadm.io][YADM]]. However this raised a few issues:
1. It was slow and clunky. Apps like [[https://discord.com][Discord]] and [[https://www.mozilla.org/en-US/firefox/new/][Firefox]] started to clutter up my =~/.config= directory, and my =.gitignore= kept growing. With nix, my config is stored in one folder, and symlinked into place
2. Applications were all configured using different languages. With home-manager for the most part I can stick to using nix,
3. Building apps was a pain, and switching laptops was getting annoying.

** Note On Installing
If you like the look of this, that's marvellous, and I'm really happy that I've
made something which you may find interesting, however:
#+begin_warning
This config is /insidious/. Copying the whole thing blindly can easily lead to
undesired effects. I recommend copying chunks instead.
#+end_warning

Oh, did I mention that I started this config when I didn't know any =nix= or =lisp=, and
this whole thing is a hack job? If you can suggest any improvements, please do
so, no matter how much criticism you include I'll appreciate it :)

** Why Nix?
Nix consists of two parts: a package manager and a language. The language is a rather simple lazy (almost) pure functional language with dynamic typing that specializes in building packages. The package manager, on the other hand, is interesting and pretty unique. It all starts with one idea.

Nix stems from the idea that FHS is fundamentally incompatible with reproducibility. Every time you see a path like =/bin/python= or =/lib/libudev.so=, there are a lot of things that you don’t know about the file that’s located there.

What’s the version of the package it came from?
What are the libraries it uses?
What configure flags were enabled during the build?
Answers to these questions can (and most likely will) change the behaviour of an application that uses those files. There are ways to get around this in FHS – for example, link directly to =/lib/libudev.so.1.6.3= or use =/bin/python3.7= in your shebang. However, there are still a lot of unknowns.

This means that if we want to get any reproducibility and consistency, FHS does not work since there is no way to infer a lot of properties of a given file.

One solution is tools like Docker, Snap, and Flatpak that create isolated FHS environments containing fixed versions of all the dependencies of a given application, and distribute those environments. However, this solution has a host of problems.

What if we want to apply different configure flags to our application or change one of the dependencies? There is no guarantee that you would be able to get the build artifact from build instructions, since putting all the build artifacts in an isolated container guarantees consistency, not reproducibility, because during build-time, tools from host’s FHS are often used, and besides the dependencies that come from other isolated environments might change.

For example, two people using the same docker image will always get the same results, but two people building the same Dockerfile can (and often do) end up with two different images.

** Drawbacks of Nix (on macOS)
The biggest issue with Nix on darwin is that NixOS (and Nix on linux) takes priority. This means:
1. Apps aren't guaranteed to build on macOS
2. External dependencies and overlays (e.g. =home-manager=) aren't guaranteed to work perfectly on darwin
3. GUI application support is almost nonexistent

MacOS is also quite locked down compared to linux, which limits the customization you can do. You also need =nix-darwin= to manage flake configurations and macOS settings. Be prepared for nix (and other package managers) to break in a future macOS update.
On top of this, =aarch64-darwin= is a Tier 4 platform, if packages that are failing the test aren't critical, they get merged. You will run into packages that don't run on m1 at all, and will likely have to PR or open an issue to get them fixed.
Lastly, remember that =aarch64-darwin= is fairly new. Especially if you use the stable channel, expect to have to build the majority of packages from source. Even if you use the unstable/master channels, you will likely end up building some packages from source

** Nix vs Homebrew, Pkgsrc, and Macports
The main package managers on macOS are:
1. [[https://github.com/NixOS/nix][Nix]]
2. [[https://www.macports.org][Macports]]
3. [[https://pkgsrc.joyent.com/install-on-osx/ ][Pkgsrc]]
4. [[https://brew.sh][Homebrew]]

#+plot: transpose:yes type:radar min:0 max:5 ticks:5 file:"./extra/assets/pm-comparison.jpeg"
| Pkg Manager    | Pkg Availability | Pkg Freshness | Ease of Use | Features | Performance |
|----------------+------------------+---------------+-------------+----------+-------------|
| Nix (Unstable) |                5 |             5 |         2.5 |      4.5 |           4 |
| Macports       |              3.5 |             3 |           3 |        3 |           4 |
| Pkgsrc         |                2 |             2 |           2 |        4 |           5 |
| Homebrew       |              3.5 |           4.5 |         4.5 |        2 |           2 |

#+attr_html: :alt Radar chart comparing my thoughts on a few macOS package managers
[[file:./extra/assets/pm-comparison.jpeg]]

Package management on macOS has a somewhat complex history, mostly owing to the fact that unlike most Linux distributions, macOS does not ship with a default package manager out of the box. It’s not surprising that one of the first projects to solve the problem of package management, Fink, was created very early, with its initial releases predating that of Mac OS X 10.0 by several months. Using Debian’s =dpkg= and =apt= as its backend, Fink is still actively maintained, though I haven’t looked at it very closely.

MacPorts, on the other hand, was released in 2002 as part of OpenDarwin, while Homebrew was released seven years later as a “solution” to many of the shortcomings that the author saw in MacPorts. Pkgsrc is an older package manager for UNIX-like systems, and supports several BSD's, as well as Linux and MacOS.  Nix is a cross-platform package manager that utilizes a purely functional deployment model where software is installed into unique directories generated through cryptographic hashes. It is also the name of the tool's programming language. A package's hash takes into account the dependencies. This package management model advertises more reliable, reproducible, and portable packages.

Homebrew makes several questionable design decisions, but one of these deserves its own section: the choice to explicitly eschew root (in fact, it will refuse to work at all if run this way). This fundamentally is a very bad idea: package managers that install software for all users of your computer, as Homebrew does by default, should always require elevated privileges to function correctly. This decision has important consequences for both security and usability, especially with the advent of System Integrity Protection in OS X El Capitan.

For quite a while, Homebrew essentially considered itself the owner of =/usr/local= (both metaphorically and literally, as it would change the permissions of the directory), to the point where it would do things like plop its README down directly into this folder. After rootless was introduced, it moved most of its files to subdirectories; however, to maintain the charade of “sudo-less” installation, Homebrew will still trash the permissions of folders inside =/usr/local=. Homebrew’s troubleshooting guide lists these out, because reinstalling macOS sets the permissions back to what they’re supposed to be and breaks Homebrew in the process.

#+begin_quote
If commands fail with permissions errors, check the permissions of /usr/local’s subdirectories. If you’re unsure what to do, you can run cd /usr/local && sudo chown -R $(whoami) bin etc include lib sbin share var opt Cellar Caskroom Frameworks.
#+end_quote

MacPorts, on the other hand, swings so far in the other direction that it’s actually borderline inconvenient to use in some sense. Philosophically, MacPorts has a very different perspective of how it should work: it tries to prevent conflicts with the system as much as possible. To achieve this, it sets up a hierarchy under =/opt= (which is the annoying bit, because this directory is not on =$PATH= by default, nor is picked up by compilers without some prodding).

Of course, this design means that there is a single shared installation is among users, so running =port= requires elevated privileges whenever performing an operation that affects all users (which, admittedly, is most of the time). MacPorts is smart about this, though: it will shed permissions and run as the =macports= user whenever possible.

In line with their stated philosophy to prevent conflicts with macOS, MacPorts will set up its own tools in isolation from those provided by the system (in fact, builds run in “sandboxes” under the =macports= user, where attempts to access files outside of the build directory–which includes system tools–are intercepted and blocked). This means MacPorts needs to install some “duplicate” tools (whereas Homebrew will try to use the ones that come with your system where possible), the downside of which is that there is an one-time “up-front” cost as it installs base packages. The upside is that this approach is significantly more contained, which makes it easier to manage and more likely to continue working as macOS changes under it.

Finally, MacPorts just seems to have a lot of thought put into it with regards to certain aspects: for example, the MacPorts Registry database is backed by SQLite by default, which makes easily introspectable in case something goes wrong. Another useful feature is built-in “livechecks” for most ports, which codify upstream version checks and make it easy to see when MacPorts’s package index need to be updated.

I won't delve too much into why I choose nix in the end (as I've covered it before), but I feel like nix takes the best of both worlds and more. You have the ease of use that homebrew provides, the sandboxing and though that was put into MacPorts, while having excellent sandboxing and the seperate =nixbld= user.

* Installing and notes
*NOTE: These are available as an executable script [[file:./extra/install.sh]]*

Install Nix. I have it setup for multi-user, but you can remove the =--daemon= if you want a single user install
#+begin_src bash :comments none :tangle "./extra/install.sh" :shebang "#!/bin/bash"
sh <(curl -L https://nixos.org/nix/install) --daemon
#+end_src
Launch an ephemeral shell with git, nixUnstable, and Emacs
#+begin_src bash :comments none :tangle "./extra/install.sh" :shebang "#!/bin/bash"
nix-shell -p nixUnstable git emacs
#+end_src
Tangle the =.org= files (not needed, but recommend in case I forgot to update tangled files)
#+begin_src bash :comments none :tangle "./extra/install.sh" :shebang "#!/bin/bash"
git clone --depth 1 https://github.com/shaunsingh/nix-darwin-dotfiles.git ~/nix-darwin-dotfiles/ && cd nix-darwin-dotfiles
emacs --batch --eval "(progn (require 'org) (setq org-confirm-babel-evaluate nil) (org-babel-tangle-file \"~/nix-darwin-dotfiles/nix-config.org\"))"
emacs --batch --eval "(progn (require 'org) (setq org-confirm-babel-evaluate nil) (org-babel-tangle-file \"~/nix-darwin-dotfiles/configs/doom/config.org\"))"
#+end_src
(if emacs asks you for comment syntax, put `# ` for everything)
Build, and switch to the dotfiles
#+begin_src bash :comments none :tangle "./extra/install.sh" :shebang "#!/bin/bash"
    nix build ~/nix-darwin-dotfiles\#darwinConfigurations.shaunsingh-laptop.system --extra-experimental-features nix-command --extra-experimental-features flakes
    ./result/sw/bin/darwin-rebuild switch --flake .#shaunsingh-laptop
#+end_src
(note, =--extra-experimental-features= is only needed the first time around. After that the configuration will edit =/etc/nix/nix.conf= to enable flakes and nix-command by default)
Symlinking with nix (and managing doom with =nix-doom-emacs=) is very finicky, so for now just manually clone my emacs config
#+begin_src bash :comments none :tangle "./extra/install.sh" :shebang "#!/bin/bash"
git clone --depth 1 https://github.com/shaunsingh/nyoom.emacs ~/.config/doom
#+end_src

Install doom emacs. No need to install all-the-icons, nix handles that!
#+begin_src bash :comments none :tangle "./extra/install.sh" :shebang "#!/bin/bash"
git clone --depth 1 https://github.com/hlissner/doom-emacs ~/.config/emacs
~/.config/emacs/bin/doom install
#+end_src

** Using Nix unstable OOTB
If you want to use nix unstable out of of the box then you can use the following script
#+begin_src bash :comments none :tangle "./extra/nix-install.sh" :shebang "#!/bin/bash"
RELEASE="nix-2.5pre20211019_4a2b7cc"
URL="https://github.com/numtide/nix-unstable-installer/releases/download/$RELEASE/install"

# install using workaround for darwin systems
if [[ $(uname -s) = "Darwin" ]]; then
    FLAG="--darwin-use-unencrypted-nix-store-volume"
fi

[[ ! -z "$1" ]] && URL="$1"

if command -v nix > /dev/null; then
    echo "nix is already installed on this system."
else
    bash <(curl -L $URL) --daemon $FLAG
fi
#+end_src

** Additional Configuration
*** Emacs
If you want to use [[https://github.com/emacs-ng/emacs-ng][Emacs-NG]], use the following build options
#+begin_src bash
  git clone --depth 1 https://github.com/emacs-ng/emacs-ng.git
  cd emacs-ng
  ./autogen.sh
  ./configure CFLAGS="-Wl,-rpath,shared,--disable-new-dtags -g -O3 -mtune=native -march=native -fomit-frame-pointer" \
              --prefix=/usr/local/ \
              --with-json --with-modules --with-compress-install \
              --with-threads --with-included-regex --with-zlib --with-libsystemd \
              --with-rsvg --with-native-compilation --with-webrender --without-javascript \
              --without-sound --without-imagemagick --without-makeinfo --without-gpm --without-dbus \
              --without-pop --without-toolkit-scroll-bars --without-mailutils --without-gsettings \
              --with-all
  make -j$(($(nproc) * 2)) NATIVE_FULL_AOT=1
  make install-strip
#+end_src

If you want to update the doom configuration, you can run
#+begin_src bash
doom upgrade
#+end_src

If you modify your shell configuration, please do run =doom env= to regenerate env vars

**** Mu4e and Gmail
Email will have a few issues, since its hardcoded to my account. Replace instances of my name and email in =~/.doom.d/config.org=
Indexed mail will go under =~/.mbsync/=, you can either manually run mbsync or use emacs to update mail.

**** Org Mode
My org mode config includes two additional plugins, org-agenda and org-roam. Both these plugins need a set directory. All org files can go under the created =~/org= dir. Roam files go under =~/org/roam=

*** Fonts
All fonts are managed via nix. Let me know if theres anything missing

*** Neovim
Run =:PackerSync= to install packer and plugins. Run =:checkhealth= to check for possible issues.
If you want to take advantage of the LSP and/or treesitter, you can install language servers and parsers using the following command:
=:LspInstall (language)=
=:TSInstall (language)=
*NOTE:* If you want to use neorg's treesitter parser on macOS, you need to link GCC to CC. Instructions [[https://github.com/nvim-neorg/neorg/issues/74#issuecomment-906627223][here]].
I also recommend installing [[https://github.com/Kethku/neovide][Neovide]]

* Flakes
** Why Flakes
Once upon a time, Nix pioneered reproducible builds: it tries hard to ensure that two builds of the same derivation graph produce an identical result. Unfortunately, the evaluation of Nix files into such a derivation graph isn’t nearly as reproducible, despite the language being nominally purely functional.

For example, Nix files can access arbitrary files (such as =~/.config/nixpkgs/config.nix=), environment variables, Git repositories, files in the Nix search path (=$NIX_PATH=), command-line arguments (=--arg=) and the system type (=builtins.currentSystem=). In other words, evaluation isn’t as hermetic as it could be. In practice, ensuring reproducible evaluation of things like NixOS system configurations requires special care.

Furthermore, there is no standard way to compose Nix-based projects. It’s rare that everything you need is in Nixpkgs; consider for instance projects that use Nix as a build tool, or NixOS system configurations. Typical ways to compose Nix files are to rely on the Nix search path (e.g. =import <nixpkgs>=) or to use =fetchGit= or =fetchTarball=. The former has poor reproducibility, while the latter provides a bad user experience because of the need to manually update Git hashes to update dependencies.

There is also no easy way to deliver Nix-based projects to users. Nix has a “channel” mechanism (essentially a tarball containing Nix files), but it’s not easy to create channels and they are not composable. Finally, Nix-based projects lack a standardized structure. There are some conventions (e.g. =shell.nix= or =release.nix=) but they don’t cover many common use cases; for instance, there is no way to discover the NixOS modules provided by a repository.

Flakes are a solution to these problems. A flake is simply a source tree (such as a Git repository) containing a file named =flake.nix= that provides a standardized interface to Nix artifacts such as packages or NixOS modules. Flakes can have dependencies on other flakes, with a “lock file” pinning those dependencies to exact revisions to ensure reproducible evaluation.

When you clone this flake and install it, your system should theoretically be the /exactly/ the same as mine, down to the commit of nixpkgs. There are also other benefits, such as that nix evaluations are cached.

** Notes on using the flake
When you install this config, there are 3 useful commands you need to know

- Updating the flake. This will update the =flake.lock= lockfile to the latest commit of nixpkgs, emacs-overlay, etc
#+begin_src bash
nix flake update
#+end_src

- Building and Installing the flake. This will first build and download everything you need, then =rebuild= your machine, so it "installs"
#+begin_src bash
nix build ~/nix-darwin-dotfiles\#darwinConfigurations.shaunsingh-laptop.system --extra-experimental-features nix-command --extra-experimental-features flakes
    ./result/sw/bin/darwin-rebuild switch --flake .#shaunsingh-laptop
#+end_src

- Testing the flake. If you have any errors when you play around with this config, then this will let you know what went wrong.
#+begin_src bash
nix flake check
#+end_src

The =flake.nix= below does the following:
1. Add a binary cache for =nix-community= overlays
2. Add inputs (=nixpkgs-master=, =nix-darwin=, =home-manager,= and =spacebar=)
3. Add overlays to get the latest versions of =neovim= (nightly) and =emacs= (emacs29)
4. Configure home-manager and the nix-daemon
5. Configure =nixpkgs= to use the overlays above
6. Configure =nix= to enable =flakes= and =nix-command= by default

For Darwin systems:
1. Create a nix-darwin configuration for my hostname
2. Source the [[file:./modules/mac.nix][mac]], [[file:./modules/home.nix][home]], and [[file:./modules/pam.nix][pam]] modules
3. Enable the use of touch-id for sudo authentication
4. add =x86-64-darwin= as a platform (to install packages through rosetta)

For Linux systems:
1. Create a nixOS configuration for my hostname
2. Source the [[file:./modules/sway.nix][sway]], [[file:./modules/home.nix][home]], and [[file:./modules/linux.nix][linux]] modules
3. Set up power-saving and network configurations

#+begin_src nix :comments none :tangle "flake.nix"
{
  description = "Shaurya's Nix Environment";

  inputs = {
    # All packages should follow latest nixpkgs/nur
    unstable.url = "github:nixos/nixpkgs/master";
    nur.url = "github:nix-community/NUR";
    # Nix-Darwin
    darwin = {
      url = "github:LnL7/nix-darwin";
      inputs.nixpkgs.follows = "unstable";
    };
    # NixOS Hardware for thinkpad config
    nixos-hardware = {
      url = "github:NixOS/nixos-hardware/master";
      inputs.nixpkgs.follows = "unstable";
    };
    # HM-manager for dotfile/user management
    home-manager = {
      url = "github:nix-community/home-manager";
      inputs.nixpkgs.follows = "unstable";
    };
    # Bar (macos)
    spacebar = {
      url = "github:shaunsingh/spacebar";
      inputs.nixpkgs.follows = "unstable";
    };
    # WM
    yabai-src = {
      url = "github:koekeishiya/yabai";
      flake = false;
    };
    # SRC for macOS emacs overlay
    emacs-src = {
      url = "github:emacs-mirror/emacs";
      flake = false;
    };
    # SRC for linux emacs overlay
    emacs-overlay = {
      url = "github:nix-community/emacs-overlay";
      inputs.nixpkgs.follows = "unstable";
    };
    # Use latest libverm to build macOS emacs build
    emacs-vterm-src = {
      url = "github:akermu/emacs-libvterm";
      flake = false;
    };
    # Keep the doom-emacs version managed with nix config
    doom-emacs = {
      url = "github:hlissner/doom-emacs";
      flake = false;
    };
    # Themeing
    base16 = {
      url = "path:./modules/base16-nix";
      inputs.nixpkgs.follows = "unstable";
    };
    # IBM-Carbon-Theme (see IBM-design: colors)
    base16-carbon-dark = {
      url = "github:shaunsingh/base16-carbon-dark";
      flake = false;
    };
    # Neovim Nightly
    neovim-overlay = {
      url = "github:nix-community/neovim-nightly-overlay";
      inputs.nixpkgs.follows = "unstable";
    };
    # Get the latest and greatest wayland tools
    nixpkgs-wayland = {
      url = "github:nix-community/nixpkgs-wayland";
      inputs.nixpkgs.follows = "unstable";
    };
    # But sway is special :)
    sway-borders-src = {
      url = "github:fluix-dev/sway-borders";
      flake = false;
    };
  };
  outputs = { self, nixpkgs, darwin, home-manager, ... }@inputs: {
    darwinConfigurations."shaunsingh-laptop" = darwin.lib.darwinSystem {
      system = "aarch64-darwin";
      modules = [
        ./modules/mac.nix
        ./modules/pam.nix
        ./modules/editors.nix
        home-manager.darwinModule
        {
          home-manager = {
            useGlobalPkgs = true;
            useUserPackages = true;
            extraSpecialArgs = {
              inherit (inputs)
                base16-carbon-dark; # pass base16 input so hm can use it
            };
            users.shauryasingh = {
              imports = [
                inputs.base16.hmModule
                ./modules/home.nix
                ./modules/theme.nix
              ];
            };
          };
        }
        ({ config, pkgs, lib, ... }: {
          services.nix-daemon.enable = true;
          security.pam.enableSudoTouchIdAuth = true;
          nixpkgs = {
            overlays = with inputs; [
              nur.overlay
              spacebar.overlay
              neovim-overlay.overlay
              (final: prev: {
                doomEmacsRevision = inputs.doom-emacs.rev;
                sf-mono-liga-bin = pkgs.callPackage ./pkgs/sf-mono-liga-bin { };
                # yabai is broken on macOS 12, so lets make a smol overlay to use the master version
                yabai = let
                  version = "4.0.0-dev";
                  buildSymlinks = prev.runCommand "build-symlinks" { } ''
                    mkdir -p $out/bin
                    ln -s /usr/bin/xcrun /usr/bin/xcodebuild /usr/bin/tiffutil /usr/bin/qlmanage $out/bin
                  '';
                in prev.yabai.overrideAttrs (old: {
                  inherit version;
                  src = inputs.yabai-src;

                  buildInputs = with prev.darwin.apple_sdk.frameworks; [
                    Carbon
                    Cocoa
                    ScriptingBridge
                    prev.xxd
                    SkyLight
                  ];

                  nativeBuildInputs = [ buildSymlinks ];
                });
                emacs-vterm = prev.stdenv.mkDerivation rec {
                  pname = "emacs-vterm";
                  version = "master";

                  src = inputs.emacs-vterm-src;

                  nativeBuildInputs = [ prev.cmake prev.libtool prev.glib.dev ];

                  buildInputs =
                    [ prev.glib.out prev.libvterm-neovim prev.ncurses ];

                  cmakeFlags = [ "-DUSE_SYSTEM_LIBVTERM=yes" ];

                  preConfigure = ''
                    echo "include_directories(\"${prev.glib.out}/lib/glib-2.0/include\")" >> CMakeLists.txt
                    echo "include_directories(\"${prev.glib.dev}/include/glib-2.0\")" >> CMakeLists.txt
                    echo "include_directories(\"${prev.ncurses.dev}/include\")" >> CMakeLists.txt
                    echo "include_directories(\"${prev.libvterm-neovim}/include\")" >> CMakeLists.txt
                  '';

                  installPhase = ''
                    mkdir -p $out
                    cp ../vterm-module.so $out
                    cp ../vterm.el $out
                  '';

                };
                emacs = (prev.emacs.override {
                  srcRepo = true;
                  nativeComp = true;
                  withSQLite3 = true;
                  withXwidgets = true;
                }).overrideAttrs (o: rec {
                  version = "29.0.50";
                  src = inputs.emacs-src;

                  buildInputs = o.buildInputs
                    ++ [ prev.darwin.apple_sdk.frameworks.WebKit ];

                  configureFlags = o.configureFlags ++ [
                    "--without-gpm"
                    "--without-dbus"
                    "--without-mailutils"
                    "--without-toolkit-scroll-bars"
                    "--without-pop"
                  ];

                  patches = [
                    ./patches/fix-window-role.patch
                    ./patches/system-appearance.patch
                    # ./patches/no-titlebar.patch
                  ];

                  postPatch = o.postPatch + ''
                    substituteInPlace lisp/loadup.el \
                    --replace '(emacs-repository-get-branch)' '"master"'
                  '';

                  postInstall = o.postInstall + ''
                    cp ${final.emacs-vterm}/vterm.el $out/share/emacs/site-lisp/vterm.el
                    cp ${final.emacs-vterm}/vterm-module.so $out/share/emacs/site-lisp/vterm-module.so
                  '';

                  CFLAGS =
                    "-DMAC_OS_X_VERSION_MAX_ALLOWED=110203 -g -O3 -mtune=native -march=native -fomit-frame-pointer";
                });
              })
            ];
          };
        })
      ];
    };
    nixosConfigurations = {
      shaunsingh-thinkpad = nixpkgs.lib.nixosSystem {
        system = "x86_64-linux";
        specialArgs = inputs;
        modules = [
          ./hardware/thinkpad-hardware-configuration.nix
          inputs.nixos-hardware.nixosModules.lenovo-thinkpad-x1
          inputs.nixos-hardware.nixosModules.lenovo-thinkpad-x1-7th-gen
          ./modules/editors.nix
          ./modules/linux.nix
          home-manager.nixosModules.home-manager
          {
            home-manager = {
              useGlobalPkgs = true;
              useUserPackages = true;
              extraSpecialArgs = {
                inherit (inputs) base16-carbon-dark; # see: theme.nix
              };
              users.shauryasingh = {
                imports = [
                  inputs.base16.hmModule
                  ./modules/home.nix
                  ./modules/theme.nix
                  ./modules/sway.nix
                ];
              };
            };
          }
          ({ config, pkgs, lib, ... }: {
            # Configure overlays
            nixpkgs = {
              overlays = with inputs; [
                nur.overlay
                neovim-overlay.overlay
                emacs-overlay.overlay
                nixpkgs-wayland.overlay
                (final: prev: {
                  doomEmacsRevision = inputs.doom-emacs.rev;
                  sf-mono-liga-bin =
                    pkgs.callPackage ./pkgs/sf-mono-liga-bin { };
                  sway-borders = let version = "1.8-borders-dev";
                  in prev.sway.overrideAttrs (old: {
                    inherit version;
                    src = inputs.sway-borders-src;
                    CFLAGS = prev.CFLAGS
                      ++ "-Wno-error"; # regular build fails because of warnings treated as errors. Disabled for now
                  });
                })
              ];
            };

            # Power management
            services.tlp.enable = true; # keep my ports controlle
            services.thermald.enable = true; # keep my battery controlled
            powerManagement.cpuFreqGovernor =
              lib.mkDefault "powersave"; # keep my cpu frequency controlled

            # Network settings.
            networking = {
              hostName = "shaunsingh-thinkpad"; # Hostname
              useDHCP = false; # Deprecated, so set explicitly to false
              wireless.enable = false;
              networkmanager.enable = true;
              firewall.enable = false; # I had issues, for some reason
            };

          })
        ];
      };
    };
  };
}
#+end_src

* Modules
** Home.nix
Home Manager allows you to use Nix’s declarative approach to manage your user-level configuration and packages. It works on any *nix system supported by Nix, including MacOS.
#+begin_src nix :comments none :tangle "./modules/home.nix"
{ pkgs, lib, config, home-manager, nix-darwin, inputs, ... }: {
#+end_src

*** Packages
My flake.nix file is getting a bit repetitive and cluttered betweeen systems. Lets move the common packages to home-manager
#+begin_src nix :comments none :tangle "./modules/home.nix"
  home.packages = with pkgs; [
    # Language Servers
    nodePackages.pyright
    rust-analyzer
  
    # Formatting
    nixfmt
    black
    shellcheck
  
    # Terminal utils and rust alternatives :tm:
    xcp
    lsd
    procs
    tree
    zoxide
    bottom
    discocss
  ];
#+end_src

*** Git
As opposed to what the xcode CLT provides, I want lfs enabled with git, and use =delta= instead of the default diff tool (rust alternatives go brr). MacOS is also quite annoying with its =.DS_Store='s everywhere, so lets ignore that
#+begin_src nix :comments none :tangle "./modules/home.nix"
  programs.git = {
    enable = true;
    userName = "shaunsingh";
    userEmail = "shaunsingh0207@gmail.com";
    delta = {
      enable = true;
      options = {
        syntax-theme = "Nord";
        line-numbers = true;
      };
    };
    ignores = [ ".dir-locals.el" ".envrc" ".DS_Store" ];
  };
#+end_src

*** Discocss
Discord is an app I use almost every day, so lets spice it up a bit. 
Ideally, I would like this to just be text and not a file, so I can use that sweet base16-nix integration, but the astrix at the start of the css seems to destroy org-mode. LMK if anyone knows of a fix
#+begin_src nix :comments none :tangle "./modules/home.nix"
  xdg.dataFile."discocss/custom.css".source = ../configs/custom.css;
#+end_src

*** Firefox
Although safari is my main browser, firefox looks very appealing with its excellent privacy and speed
#+begin_src nix :comments none :tangle "./modules/home.nix"
  programs.firefox = {
    enable = true;
    extensions = with pkgs.nur.repos.rycee.firefox-addons; [
      ublock-origin
      tridactyl
      reddit-enhancement-suite
      betterttv
    ];
  };
#+end_src

Now for the configuration. We want firefox to use our nice base16-nix powered css, and we want to configure the UI. Lets also enable the (rust powered ftw) webrender/servo renderer.
#+begin_src nix :comments none :tangle "./modules/home.nix"
  programs.firefox.profiles = let
    userChrome = with config.lib.base16.theme; ''
      :root {
      --window-colour:                #${base01-hex};
      --secondary-colour:             #${base04-hex};
      --inverted-colour:              #${base05-hex};

      --uc-identity-color-blue:       #${base08-hex};
      --uc-identity-color-turquoise:  #${base09-hex};
      --uc-identity-color-green:      #${base0A-hex};
      --uc-identity-color-yellow:     #${base0B-hex};
      --uc-identity-color-orange:     #${base0C-hex};
      --uc-identity-color-red:        #${base0D-hex};
      --uc-identity-color-pink:       #${base0E-hex};
      --uc-identity-color-purple:     #${base0F-hex};
      
      /* URL colour in URL bar suggestions */
      --urlbar-popup-url-color: var(--uc-identity-color-purple) !important;

      /* global border radius */
      --uc-border-radius: 0;

      /* dynamic url bar width settings */
      --uc-urlbar-width: clamp(200px, 40vw, 500px);

      /* dynamic tab width settings */
      --uc-active-tab-width:   clamp(100px, 20vw, 300px);
      --uc-inactive-tab-width: clamp( 50px, 15vw, 200px);

      /* if active only shows the tab close button on hover*/
      --show-tab-close-button-hover: none; /* DEFAULT: -moz-inline-box; */

      /* adds left and right margin to the container-tabs indicator */
      --container-tabs-indicator-margin: 10px; }

      #back-button,
      #forward-button { display: none !important; }

      /* bookmark icon */
      #star-button{ display: none !important; }

      /* zoom indicator */
      #urlbar-zoom-button { display: none !important; }

      /* Make button small as Possible, hidden out of sight */
      #PanelUI-button { margin-top: -5px; margin-bottom: 44px; }

      #PanelUI-menu-button {
      padding: 0px !important;
      max-height: 1px;

      list-style-image: none !important;
      }

      #PanelUI-menu-button .toolbarbutton-icon { width: 1px !important; }
      #PanelUI-menu-button .toolbarbutton-badge-stack { padding: 0px !important; }

      #reader-mode-button{ display: none !important; }

      /* tracking protection shield icon */
      #tracking-protection-icon-container { display: none !important; }

      /* #identity-box { display: none !important } /* hides encryption AND permission items */
      #identity-permission-box { display: none !important; } /* only hodes permission items */

      /* e.g. playing indicator (secondary - not icon) */
      .tab-secondary-label { display: none !important; }

      #pageActionButton { display: none !important; }
      #page-action-buttons { display: none !important; }

      #urlbar-go-button { display: none !important; }

      :root {

      --uc-theme-colour:                          var(--window-colour);
      --uc-hover-colour:                          var(--secondary-colour);
      --uc-inverted-colour:                       var(--inverted-colour);

      --button-bgcolor:                           var(--uc-theme-colour)    !important;
      --button-hover-bgcolor:                     var(--uc-hover-colour)    !important;
      --button-active-bgcolor:                    var(--uc-hover-colour)    !important;

      --toolbar-bgcolor:                          var(--uc-theme-colour)    !important;
      --toolbarbutton-hover-background:           var(--uc-hover-colour)    !important;
      --toolbarbutton-active-background:          var(--uc-hover-colour)    !important;
      --toolbarbutton-border-radius:              var(--uc-border-radius)   !important;
      --lwt-toolbar-field-focus:                  var(--uc-theme-colour)    !important;
      --toolbarbutton-icon-fill:                  var(--uc-inverted-colour) !important;
      --toolbar-field-focus-background-color:     var(--secondary-colour)   !important;
      --toolbar-field-color:                      var(--uc-inverted-colour) !important;
      --toolbar-field-focus-color:                var(--uc-inverted-colour) !important;

      --tabs-border-color:                        var(--uc-theme-colour)    !important;
      --tab-border-radius:                        var(--uc-border-radius)   !important;
      --lwt-text-color:                           var(--uc-inverted-colour) !important;
      --lwt-tab-text:                             var(--uc-inverted-colour) !important;

      --lwt-sidebar-background-color:             var(--uc-hover-colour)    !important;
      --lwt-sidebar-text-color:                   var(--uc-inverted-colour) !important;

      --arrowpanel-border-color:                  var(--uc-theme-colour)    !important;
      --arrowpanel-border-radius:                 var(--uc-border-radius)   !important;
      --arrowpanel-background:                    var(--uc-theme-colour)    !important;
      --arrowpanel-color:                         var(--inverted-colour)    !important;

      --autocomplete-popup-highlight-background:  var(--uc-inverted-colour) !important;
      --autocomplete-popup-highlight-color:       var(--uc-inverted-colour) !important;
      --autocomplete-popup-hover-background:      var(--uc-inverted-colour) !important;


      --tab-block-margin: 2px !important;

      }

      window,
      #main-window,
      #toolbar-menubar,
      #TabsToolbar,
      #PersonalToolbar,
      #navigator-toolbox,
      #sidebar-box,
      #nav-bar {

      -moz-appearance: none !important;

      border: none !important;
      box-shadow: none !important;
      background: var(--uc-theme-colour) !important;

      }

      /* grey out ccons inside the toolbar to make it
      - more aligned with the Black & White colour look */
      #PersonalToolbar toolbarbutton:not(:hover),
      #bookmarks-toolbar-button:not(:hover) { filter: grayscale(1) !important; }


      /* remove window control buttons */
      .titlebar-buttonbox-container { display: none !important; }


      /* remove "padding" left and right from tabs */
      .titlebar-spacer { display: none !important; }

      /* remove gap after pinned tabs */
      #tabbrowser-tabs[haspinnedtabs]:not([positionpinnedtabs])
          > #tabbrowser-arrowscrollbox
          > .tabbrowser-tab[first-visible-unpinned-tab] { margin-inline-start: 0 !important; }


      /* remove tab shadow */
      .tabbrowser-tab
          >.tab-stack
          > .tab-background { box-shadow: none !important;  }


      /* tab background */
      .tabbrowser-tab
          > .tab-stack
          > .tab-background { background: var(--uc-theme-colour) !important; }


      /* active tab background */
      .tabbrowser-tab[selected]
          > .tab-stack
          > .tab-background { background: var(--uc-hover-colour) !important; }


      /* multi tab selection */
      #tabbrowser-tabs:not([noshadowfortests]) .tabbrowser-tab:is([visuallyselected=true], [multiselected]) > .tab-stack > .tab-background:-moz-lwtheme { background: var(--uc-hover-colour) !important; }


      /* tab close button options */
      .tabbrowser-tab:not([pinned]) .tab-close-button { display: var(--show-tab-close-button) !important; }
      .tabbrowser-tab:not([pinned]):hover .tab-close-button { display: var(--show-tab-close-button-hover) !important }


      /* adaptive tab width */
      .tabbrowser-tab[selected][fadein]:not([pinned]) { max-width: var(--uc-active-tab-width) !important; }
      .tabbrowser-tab[fadein]:not([selected]):not([pinned]) { max-width: var(--uc-inactive-tab-width) !important; }


      /* container tabs indicator */
      .tabbrowser-tab[usercontextid]
          > .tab-stack
          > .tab-background
          > .tab-context-line {

      margin: -1px var(--container-tabs-indicator-margin) 0 var(--container-tabs-indicator-margin) !important;

      border-radius: var(--tab-border-radius) !important;

      }


      /* show favicon when media is playing but tab is hovered */
      .tab-icon-image:not([pinned]) { opacity: 1 !important; }


      /* Makes the speaker icon to always appear if the tab is playing (not only on hover) */
      .tab-icon-overlay:not([crashed]),
      .tab-icon-overlay[pinned][crashed][selected] {

      top: 5px !important;
      z-index: 1 !important;

      padding: 1.5px !important;
      inset-inline-end: -8px !important;
      width: 16px !important; height: 16px !important;

      border-radius: 10px !important;

      }

      /* style and position speaker icon */
      .tab-icon-overlay:not([sharing], [crashed]):is([soundplaying], [muted], [activemedia-blocked]) {

      stroke: transparent !important;
      background: transparent !important;
      opacity: 1 !important; fill-opacity: 0.8 !important;

      color: currentColor !important;

      stroke: var(--uc-theme-colour) !important;
      background-color: var(--uc-theme-colour) !important;

      }


      /* change the colours of the speaker icon on active tab to match tab colours */
      .tabbrowser-tab[selected] .tab-icon-overlay:not([sharing], [crashed]):is([soundplaying], [muted], [activemedia-blocked]) {

      stroke: var(--uc-hover-colour) !important;
      background-color: var(--uc-hover-colour) !important;

      }


      .tab-icon-overlay:not([pinned], [sharing], [crashed]):is([soundplaying], [muted], [activemedia-blocked]) { margin-inline-end: 9.5px !important; }


      .tabbrowser-tab:not([image]) .tab-icon-overlay:not([pinned], [sharing], [crashed]) {

      top: 0 !important;

      padding: 0 !important;
      margin-inline-end: 5.5px !important;
      inset-inline-end: 0 !important;

      }


      .tab-icon-overlay:not([crashed])[soundplaying]:hover,
      .tab-icon-overlay:not([crashed])[muted]:hover,
      .tab-icon-overlay:not([crashed])[activemedia-blocked]:hover {

      color: currentColor !important;
      stroke: var(--uc-inverted-colour) !important;
      background-color: var(--uc-inverted-colour) !important;
      fill-opacity: 0.95 !important;

      }


      .tabbrowser-tab[selected] .tab-icon-overlay:not([crashed])[soundplaying]:hover,
      .tabbrowser-tab[selected] .tab-icon-overlay:not([crashed])[muted]:hover,
      .tabbrowser-tab[selected] .tab-icon-overlay:not([crashed])[activemedia-blocked]:hover {

      color: currentColor !important;
      stroke: var(--uc-inverted-colour) !important;
      background-color: var(--uc-inverted-colour) !important;
      fill-opacity: 0.95 !important;

      }


      /* speaker icon colour fix */
      #TabsToolbar .tab-icon-overlay:not([crashed])[soundplaying],
      #TabsToolbar .tab-icon-overlay:not([crashed])[muted],
      #TabsToolbar .tab-icon-overlay:not([crashed])[activemedia-blocked] { color: var(--uc-inverted-colour) !important; }


      /* speaker icon colour fix on hover */
      #TabsToolbar .tab-icon-overlay:not([crashed])[soundplaying]:hover,
      #TabsToolbar .tab-icon-overlay:not([crashed])[muted]:hover,
      #TabsToolbar .tab-icon-overlay:not([crashed])[activemedia-blocked]:hover { color: var(--uc-theme-colour) !important; }

      #nav-bar {

      border:     none !important;
      box-shadow: none !important;
      background: transparent !important;

      }


      /* remove border below whole nav */
      #navigator-toolbox { border-bottom: none !important; }


      #urlbar,
      #urlbar * {

      outline: none !important;
      box-shadow: none !important;

      }


      #urlbar-background { border: var(--uc-hover-colour) !important; }


      #urlbar[focused="true"]
          > #urlbar-background,
      #urlbar:not([open])
          > #urlbar-background { background: transparent !important; }


      #urlbar[open]
          > #urlbar-background { background: var(--uc-theme-colour) !important; }


      .urlbarView-row:hover
          > .urlbarView-row-inner,
      .urlbarView-row[selected]
          > .urlbarView-row-inner { background: var(--uc-hover-colour) !important; }


      /* transition to oneline */
      @media (min-width: 1000px) {


      /* move tabs bar over */
      #TabsToolbar { margin-left: var(--uc-urlbar-width) !important; }


      /* move entire nav bar  */
      #nav-bar { margin: calc((var(--urlbar-min-height) * -1) - 8px) calc(100vw - var(--uc-urlbar-width)) 0 0 !important; }


      } /* end media query */

      /* Container Tabs */
      .identity-color-blue      { --identity-tab-color: var(--uc-identity-color-blue)      !important; --identity-icon-color: var(--uc-identity-color-blue)      !important; }
      .identity-color-turquoise { --identity-tab-color: var(--uc-identity-color-turquoise) !important; --identity-icon-color: var(--uc-identity-color-turquoise) !important; }
      .identity-color-green     { --identity-tab-color: var(--uc-identity-color-green)     !important; --identity-icon-color: var(--uc-identity-color-green)     !important; }
      .identity-color-yellow    { --identity-tab-color: var(--uc-identity-color-yellow)    !important; --identity-icon-color: var(--uc-identity-color-yellow)    !important; }
      .identity-color-orange    { --identity-tab-color: var(--uc-identity-color-orange)    !important; --identity-icon-color: var(--uc-identity-color-orange)    !important; }
      .identity-color-red       { --identity-tab-color: var(--uc-identity-color-red)       !important; --identity-icon-color: var(--uc-identity-color-red)       !important; }
      .identity-color-pink      { --identity-tab-color: var(--uc-identity-color-pink)      !important; --identity-icon-color: var(--uc-identity-color-pink)      !important; }
      .identity-color-purple    { --identity-tab-color: var(--uc-identity-color-purple)    !important; --identity-icon-color: var(--uc-identity-color-purple)    !important; }
    '';
    settings = {
      "browser.startup.homepage" = "https://searx.tiekoetter.com/";
      "browser.ctrlTab.recentlyUsedOrder" = false;
      "browser.newtabpage.enabled" = false;
      "browser.bookmarks.showMobileBookmarks" = true;
      "browser.uidensity" = 1;
      "browser.urlbar.placeholderName" = "Search Using SearXNG";
      "browser.urlbar.update1" = true;
      "privacy.trackingprotection.enabled" = true;
      "privacy.trackingprotection.socialtracking.enabled" = true;
      "privacy.trackingprotection.socialtracking.annotate.enabled" = true;
      "services.sync.declinedEngines" = "addons,prefs";
      "services.sync.engine.addons" = false;
      "services.sync.engineStatusChanged.addons" = true;
      "services.sync.engine.prefs" = false;
      "services.sync.engineStatusChanged.prefs" = true;
      "gfx.webrender.all" = true;
      "trim_on_minimize" = true;
      "toolkit.legacyUserProfileCustomizations.stylesheets" = true;
    };
  in {
    home = {
      inherit settings;
      inherit userChrome;
      id = 0;
    };
  };
#+end_src

*** Alacritty
Alacritty is my terminal emulator of choice. Similar to firefox, we want to create a fake package, and then configure it as normal
#+begin_src nix :comments none :tangle "./modules/home.nix"
  programs.alacritty = {
    enable = true;
    settings = with config.lib.base16.theme; {
      window.padding.x = 45;
      window.padding.y = 45;
      window.decorations = "none";
      window.dynamic_title = true;
      live_config_reload = true;
      mouse.hide_when_typing = true;
      use_thin_strokes = true;
      cursor.style = "Beam";

      font = {
        size = 15;
        normal.family = "Liga SFMono Nerd Font";
        normal.style = "Light";
        bold.family = "Liga SFMono Nerd Font";
        bold.style = "Bold";
        italic.family = "Liga SFMono Nerd Font";
        italic.style = "Italic";
      };

      colors = {
        cursor.cursor = "#${base04-hex}";
        primary.background = "#${base00-hex}";
        primary.foreground = "#${base06-hex}";
        normal = {
          black =   "#${base00-hex}";
          red =     "#${base0B-hex}";
          green =   "#${base0C-hex}";
          yellow =  "#${base0D-hex}";
          blue =    "#${base07-hex}";
          magenta = "#${base0F-hex}";
          cyan =    "#${base09-hex}";
          white =   "#${base04-hex}";
        };
        bright = {
          black =   "#${base03-hex}";
          red =     "#${base0B-hex}";
          green =   "#${base0C-hex}";
          yellow =  "#${base0D-hex}";
          blue =    "#${base07-hex}";
          magenta = "#${base0F-hex}";
          cyan =    "#${base09-hex}";
          white =   "#${base06-hex}";
        };
      };
    };
  };
#+end_src

*** Fish
**** Aliases
I also like to alias common commands with other, better rust alternatives :tm:
#+begin_src nix :comments none :tangle "./modules/home.nix"
programs.fish.enable = true;
programs.fish.shellAliases = with pkgs; {
    ":q" = "exit";
    vi = "emacsclient -c";
    git-rebase = "git rebase -i HEAD~2";
    ls = "lsd";
    ps = "ps";
    tree = "tree -a -C";
    cat = "bat";
    top = "btm";
    cp = "xcp";
    find = "fd";
  };
#+end_src

**** Init
I like to make my prompt look pretty (along with some =nix-shell= and =git= integration)
#+begin_src nix :comments none :tangle "./modules/home.nix"
  programs.fish.interactiveShellInit = ''
    set -g fish_greeting ""
    zoxide init fish --cmd cd | source
    set -x PATH ~/.config/emacs/bin $PATH

    set -g fish_greeting ""
    set -U fish_color_autosuggestion      brblack
    set -U fish_color_cancel              -r
    set -U fish_color_command             green
    set -U fish_color_comment             magenta
    set -U fish_color_cwd                 green
    set -U fish_color_cwd_root            red
    set -U fish_color_end                 magenta
    set -U fish_color_error               red
    set -U fish_color_escape              cyan
    set -U fish_color_history_current     --bold
    set -U fish_color_host                normal
    set -U fish_color_normal              normal
    set -U fish_color_operator            cyan
    set -U fish_color_param               blue
    set -U fish_color_quote               yellow
    set -U fish_color_redirection         yellow
    set -U fish_color_search_match        'yellow' '--background=brightblack'
    set -U fish_color_selection           'white' '--bold' '--background=brightblack'
    set -U fish_color_status              red
    set -U fish_color_user                green
    set -U fish_color_valid_path          --underline
    set -U fish_pager_color_completion    normal
    set -U fish_pager_color_description   yellow
    set -U fish_pager_color_prefix        'white' '--bold' '--underline'
    set -U fish_pager_color_progress      'white' '--background=cyan'

    # prompt
    set fish_prompt_pwd_dir_length 1
    set __fish_git_prompt_show_informative_status 1

    set fish_color_command green
    set fish_color_param $fish_color_normal

    set __fish_git_prompt_showdirtystate 'yes'
    set __fish_git_prompt_showupstream 'yes'

    set __fish_git_prompt_color_branch brown
    set __fish_git_prompt_color_dirtystate red
    set __fish_git_prompt_color_stagedstate yellow
    set __fish_git_prompt_color_upstream cyan
    set __fish_git_prompt_color_cleanstate green
    set __fish_git_prompt_color_invalidstate red

    set __fish_git_prompt_char_dirtystate ' ✕ '
    set __fish_git_prompt_char_stateseparator ' '
    set __fish_git_prompt_char_untrackedfiles '++'
    set __fish_git_prompt_char_cleanstate ' ✓ '
    set __fish_git_prompt_char_stagedstate '-> '
    set __fish_git_prompt_char_conflictedstate "✕ "

    set __fish_git_prompt_char_upstream_prefix ""
    set __fish_git_prompt_char_upstream_equal ""
    set __fish_git_prompt_char_upstream_ahead ' >= '
    set __fish_git_prompt_char_upstream_behind ' <= '
    set __fish_git_prompt_char_upstream_diverged ' <=> '

    function _print_in_color
      set -l string $argv[1]
      set -l color  $argv[2]

      set_color $color
      printf $string
      set_color normal
    end

    function _prompt_color_for_status
      if test $argv[1] -eq 0
        echo magenta
      else
        echo red
      end
    end

    function fish_prompt
        set -l last_status $status

        set -l nix_shell_info (
          if test -n "$IN_NIX_SHELL"
            echo -n " [nix-shell]"
          end
        )

        if test $HOME != $PWD
            _print_in_color ""(prompt_pwd) blue
        end
        __fish_git_prompt " (%s)"

        _print_in_color "$nix_shell_info λ " (_prompt_color_for_status $last_status) ]

    end
  '';
#+end_src

*** Bat
Bat is another rust alternative :tm: to cat, and provides syntax highlighting. Lets theme it to match nord
#+begin_src nix :comments none :tangle "./modules/home.nix"
  programs.bat = {
    enable = true;
    config = { theme = "Nord"; };
  };
#+end_src

*** Tmux
Lastly, lets make tmux look just as pretty as our prompt, and enable truecolor support.
#+begin_src nix :comments none :tangle "./modules/home.nix"
  programs.tmux.enable = true;
  programs.tmux.extraConfig = ''
    # make sure fish works in tmux
    set -g default-terminal "screen-256color"
    set -sa terminal-overrides ',xterm-256color:RGB'
    # so that escapes register immidiately in vim
    set -sg escape-time 1
    set -g focus-events on
    # mouse support
    set -g mouse on
    # change prefix to C-a
    set -g prefix C-a
    bind C-a send-prefix
    unbind C-b
    # extend scrollback
    set-option -g history-limit 5000
    # vim-like pane resizing
    bind -r C-k resize-pane -U
    bind -r C-j resize-pane -D
    bind -r C-h resize-pane -L
    bind -r C-l resize-pane -R
    # vim-like pane switching
    bind -r k select-pane -U
    bind -r j select-pane -D
    bind -r h select-pane -L
    bind -r l select-pane -R
    # styling
    set -g status-style fg=black,bg=default
    set -g status-left ""
    set -g status-right ""
    set -g status-justify centre
    set -g status-position bottom
    set -g pane-active-border-style bg=default,fg=default
    set -g pane-border-style fg=default
    set -g window-status-current-format "#[fg=cyan]#[fg=white]#[bg=cyan]#I #[bg=brightwhite]#[fg=black] #W#[fg=brightwhite]#[bg=default] #[bg=default] #[fg=magenta]#[fg=white]#[bg=magenta]λ #[fg=black]#[bg=brightwhite] %a %d %b #[fg=magenta]%R#[fg=brightwhite]#[bg=default]"
    set -g window-status-format "#[fg=magenta]#[fg=white]#[bg=magenta]#I #[bg=brightwhite]#[fg=black] #W#[fg=brightwhite]#[bg=default] "
  '';
}
#+end_src

** Editors.nix
My editor configurations have grown out of control, so lets tame them a bit.
#+begin_src nix :comments none :tangle "./modules/editors.nix"
{ pkgs, lib, home-manager, ... }:
#+end_src

*** Doom-emacs
Nix via doom-emacs is very, /very/ annoying. Initially I was using [[https://github.com/vlaci/nix-doom-emacs][Nix-doom-emacs]]. However, this has a few drawbacks
1. It doesn't support straight =:recipe=, so all packages must be from melpa or elpa
2. It pins the version of doom, so you need to update doom and its dependencies painstakingly manually
3. It just ends up breaking anyways.

A simpler solution is just to have nix clone =doom-emacs= to =~/.config/emacs=, and the user can handle doom manually. The doom version is pinned in the =flake.lock= of the repository, but a =doom upgrade= will override it. Since I have an arm based macbook, I also use nix to install and manage my treesitter parsers instead of compiling them myself
#+begin_src nix :comments none :tangle "./modules/editors.nix"
let
  emacsSyncScript = pkgs.writeScriptBin "doom-sync-git" ''
    #!${pkgs.runtimeShell}
    export PATH=$PATH:${lib.makeBinPath [ pkgs.git pkgs.sqlite pkgs.unzip ]}
    if [ ! -d $HOME/.config/emacs/.git ]; then
      mkdir -p $HOME/.config/emacs
      git -C $HOME/.config/emacs init
    fi
    if [ $(git -C $HOME/.config/emacs rev-parse HEAD) != ${pkgs.doomEmacsRevision} ]; then
      git -C $HOME/.config/emacs fetch https://github.com/hlissner/doom-emacs.git || true
      git -C $HOME/.config/emacs checkout ${pkgs.doomEmacsRevision} || true
    fi
  '';
in
{
  home-manager.users.shauryasingh.home.packages = with pkgs; [
    (ripgrep.override { withPCRE2 = true; })
    fd
    sqlite
    gnuplot
    pandoc
    # sdcv
    (aspellWithDicts (ds: with ds; [ en en-computers en-science ]))
    tectonic
    emacsSyncScript
    # mu
    # isync
    languagetool
    neovim-nightly
    # neovide
    # nodejs-16_x
  ];
}
#+end_src

** Mac.nix
There are mac-specific tweaks I need to do. In the future if I switch to nixOS full-time, then I wuold likely need to remove the mac-specific packages. An easy way to do this is just keep them in a seperate file:
#+begin_src nix :comments none :tangle "./modules/mac.nix"
{ config, pkgs, lib, ... }: {
#+end_src

** Basic config 
Just the nix-y stuff
#+begin_src nix :comments none :tangle "./modules/mac.nix"
  nix = {
    package = pkgs.nixUnstable;
    extraOptions = ''
      system = aarch64-darwin # M1 gang
      extra-platforms = aarch64-darwin x86_64-darwin # But we use rosetta too
      experimental-features = nix-command flakes
      build-users-group = nixbld
    '';
  };
  programs.fish.enable = true;
  environment.shells = with pkgs; [ fish ];
  users.users.shauryasingh = {
    home = "/Users/shauryasingh";
    shell = pkgs.fish;
  };
  system.activationScripts.postActivation.text = ''
    # Set the default shell as fish for the user. MacOS doesn't do this like nixOS does
    sudo chsh -s ${lib.getBin pkgs.fish}/bin/fish shauryasingh
  '';
  environment.systemPackages = with pkgs; [ emacs ];
  fonts = {
    enableFontDir = true;
    fonts = with pkgs; [
      ibm-plex
      emacs-all-the-icons-fonts
      sf-mono-liga-bin
    ];
  };
#+end_src

*** Yabai
Yabai is my tiling WM of choice. As this is an m1 (=aarch64-darwin=) laptop, I use the =the-future= branch, which enables the SA addon on m1 machines and monterey support

Now to configure the package via nix
#+begin_src nix :comments none :tangle "./modules/mac.nix"
  services.yabai = {
    enable = true;
    enableScriptingAddition = true;
    package = pkgs.yabai;
    config = {
      # layout
      layout = "bsp";
      auto_balance = "on";
      split_ratio = "0.50";
      window_placement = "second_child";
      # Gaps
      window_gap = 18;
      top_padding = 18;
      bottom_padding = 46;
      left_padding = 18;
      right_padding = 18;
      # shadows and borders
      window_shadow = "on";
      window_border = "off";
      window_border_width = 3;
      window_opacity = "on";
      window_opacity_duration = "0.1";
      active_window_opacity = "1.0";
      normal_window_opacity = "1.0";
      # mouse
      mouse_modifier = "cmd";
      mouse_action1 = "move";
      mouse_action2 = "resize";
      mouse_drop_action = "swap";
    };
    extraConfig = ''
      # rules
      yabai -m rule --add app=emacs-29.0.50 manage=on
      yabai -m rule --add app='Firefox Nightly' manage=on
      yabai -m rule --add app='System Preferences' manage=off
      yabai -m rule --add app='Activity Monitor' manage=off
    '';
  };
#+end_src

*** Spacebar
Spacebar is my bar of choice on macOS. Its lighter than any web-based ubersicht bar, and looks nice
#+begin_src nix :comments none :tangle "./modules/mac.nix"
  services.spacebar = {
    enable = true;
    package = pkgs.spacebar;
    config = {
      position = "bottom";
      height = 28;
      title = "on";
      spaces = "on";
      power = "on";
      clock = "off";
      right_shell = "off";
      padding_left = 20;
      padding_right = 20;
      spacing_left = 25;
      spacing_right = 25;
      text_font = ''"Menlo:16.0"'';
      icon_font = ''"Menlo:16.0"'';
      # Light theme colors:
      ## background_color = "0xffffffff";
      ## foreground_color = "0xff37474F";
      ## space_icon_color = "0xff673AB7";
      # Dark theme colors:
      background_color = "0xff2E3440";
      foreground_color = "0xffECEFF4";
      space_icon_color = "0xff81A1C1";
      power_icon_strip = " ";
      space_icon_strip = "一 二 三 四 五 六 七 八 九 十";
      spaces_for_all_displays = "on";
      display_separator = "on";
      display_separator_icon = "|";
      clock_format = ''"%d/%m/%y %R"'';
      right_shell_icon = " ";
      right_shell_command = "whoami";
    };
  };
#+end_src

*** SKHD
Skhd is the hotkey daemon for yabai. As yabai is disabled, it makes sense to disable skhd too for the time being
#+begin_src nix :comments none :tangle "./modules/mac.nix"
  services.skhd = {
    enable = true;
    package = pkgs.skhd;
    skhdConfig = ''
      # open terminal
      cmd - return : alacritty

      # open emacs
      cmd - e : emacs
      cmd + lalt -e : emacsclient --eval "(emacs-everywhere)"
      cmd + shift -e : emacsclient --eval "(emacs-everywhere)"

      # focus window
      lalt - h : yabai -m window --focus west
      lalt - j : yabai -m window --focus south
      lalt - k : yabai -m window --focus north
      lalt - l : yabai -m window --focus east
          
      # swap managed window
      shift + lalt - h : yabai -m window --swap west
      shift + lalt - l : yabai -m window --swap east
      shift + lalt - j : yabai -m window --swap south
      shift + lalt - k : yabai -m window --swap north

      # focus spaces
      alt - x : yabai -m space --focus recent
      alt - 1 : yabai -m space --focus 1
      alt - 2 : yabai -m space --focus 2
      alt - 3 : yabai -m space --focus 3
      alt - 4 : yabai -m space --focus 4
      alt - 5 : yabai -m space --focus 5
      alt - 6 : yabai -m space --focus 6
      alt - 7 : yabai -m space --focus 7
      alt - 8 : yabai -m space --focus 8

      # focus on next/prev space
      alt + ctrl - q : yabai -m space --focus prev
      alt + ctrl - e : yabai -m space --focus next

      # send window to desktop
      shift + alt - x : yabai -m window --space recent
      shift + alt - 1 : yabai -m window --space 1
      shift + alt - 2 : yabai -m window --space 2
      shift + alt - 3 : yabai -m window --space 3
      shift + alt - 4 : yabai -m window --space 4
      shift + alt - 5 : yabai -m window --space 5
      shift + alt - 6 : yabai -m window --space 6
      shift + alt - 7 : yabai -m window --space 7
      shift + alt - 8 : yabai -m window --space 8

      # float / unfloat window and center on screen
      lalt - t : yabai -m window --toggle float;\
                 yabai -m window --grid 4:4:1:1:2:2

      # toggle window zoom
      lalt - d : yabai -m window --toggle zoom-parent

   '';
  };
#+end_src

*** Homebrew
GUI apps with Nix are finicky at best. As much as I would like to fully give up homebrew, its very annoying having to re-install GUI apps on new systems
#+begin_src nix :comments none :tangle "./modules/mac.nix"
homebrew = {
    brewPrefix = "/opt/homebrew/bin";
    enable = true;
    autoUpdate = true;
    cleanup = "zap"; # keep it clean
    global = {
      brewfile = true;
      noLock = true;
    };

    taps = [
      "homebrew/core" # core
      "homebrew/cask" # we're using this for casks, after all
      "homebrew/cask-versions" # needed for firefox-nightly and discord-canary
    ];

    casks = [
      "discord-canary" # discord for macOS
      "firefox-nightly" # my browser of choice
      "nvidia-geforce-now" # game streaming
      "via" # keyboard config
      "blender" # blender
      "prusaslicer" # 3d printing
      "balenaetcher" # flashing usbs/sd
    ];
  };
#+end_src

*** MacOS Settings
I like my hostname to be the same as the flake's target
#+begin_src nix :comments none :tangle "./modules/mac.nix"
  networking.hostName = "shaunsingh-laptop";
  system.stateVersion = 4;
#+end_src

Along with that, lets
- Increase key repeat rate
- Remap Caps to Esc
- Save screenshots to =/tmp=
- Autohide the dock and menubar
- Show extensions in Finder (and allow it to "quit")
- Set macOS to use the dark theme
- Configure Trackpad and mouse behavior
- Enable subpixel antialiasing on internal/external displays
#+begin_src nix :comments none :tangle "./modules/mac.nix"
  system.keyboard = {
    enableKeyMapping = true;
    remapCapsLockToEscape = true;
  };
  system.defaults = {
    screencapture = { location = "/tmp"; };
    dock = {
      autohide = true;
      showhidden = true;
      mru-spaces = false;
    };
    finder = {
      AppleShowAllExtensions = true;
      QuitMenuItem = true;
      FXEnableExtensionChangeWarning = true;
    };
    NSGlobalDomain = {
      AppleKeyboardUIMode = 3;
      ApplePressAndHoldEnabled = false;
      AppleFontSmoothing = 1;
      _HIHideMenuBar = true;
      InitialKeyRepeat = 10;
      KeyRepeat = 1;
      "com.apple.mouse.tapBehavior" = 1;
      "com.apple.swipescrolldirection" = true;
    };
  };
}
#+end_src

** Pam.nix
Apple's touchid is an excellent way of authenticating anything quickly and securely. Sadly, =sudo= doesn't support it by default, but its an easy fix. T do this, we edit =/etc/pam.d/sudo= via =sed= to include the relevent code to enable touchid.

We don't use =environment.etc= because this would require that the user manually delete
=/etc/pam.d/sudo= which seems unwise given that applying the nix-darwin configuration requires
=sudo=. We also can't use =system.patches= since it only runs once, and so won't patch in the
changes again after OS updates (which remove modifications to this file).

As such, we resort to line addition/deletion in place using =sed=. We add a comment to the
added line that includes the name of the option, to make it easier to identify the line that
should be deleted when the option is disabled.

#+begin_src nix :comments none :tangle "./modules/pam.nix"
{ config, lib, pkgs, ... }:

with lib;

let
  cfg = config.security.pam;
  mkSudoTouchIdAuthScript = isEnabled:
    let
      file = "/etc/pam.d/sudo";
      option = "security.pam.enableSudoTouchIdAuth";
    in ''
      ${if isEnabled then ''
        # Enable sudo Touch ID authentication, if not already enabled
        if ! grep 'pam_tid.so' ${file} > /dev/null; then
          sed -i "" '2i\
        auth       sufficient     pam_tid.so # nix-darwin: ${option}
          ' ${file}
        fi
      '' else ''
        # Disable sudo Touch ID authentication, if added by nix-darwin
        if grep '${option}' ${file} > /dev/null; then
          sed -i "" '/${option}/d' ${file}
        fi
      ''}
    '';

in {
  options = {
    security.pam.enableSudoTouchIdAuth = mkEnableOption ''
      Enable sudo authentication with Touch ID
      When enabled, this option adds the following line to /etc/pam.d/sudo:
          auth       sufficient     pam_tid.so
      (Note that macOS resets this file when doing a system update. As such, sudo
      authentication with Touch ID won't work after a system update until the nix-darwin
      configuration is reapplied.)
    '';
  };

  config = {
    system.activationScripts.extraActivation.text = ''
      # PAM settings
      echo >&2 "setting up pam..."
      ${mkSudoTouchIdAuthScript cfg.enableSudoTouchIdAuth}
    '';
  };
}
#+end_src

** Sway.nix
On the linux side of things, I need to configure sway and all my linux utilities, such as: 
- Install the required packages
- Enable sway/xwayland/gtk
- Set the wallpaper and theme for sway 
- Enable trackpad and keyboard support
- Set enviornment variables
- Use sway-borders (maybe)
- Setup foot terminal
- Configure firefox to run wayland native
#+begin_src nix :comments none :tangle "./modules/sway.nix"
{ config, options, pkgs, lib, ... }: {
  home.packages = with pkgs; [
    # Build Emacs with Vterm
    ((emacsPackagesNgGen emacsPgtk).emacsWithPackages (epkgs: [ epkgs.vterm ]))
    # Wayland tools
    wofi
    grim
    wl-clipboard
    brightnessctl
  ];
  wayland.windowManager.sway = {
    enable = true;
    # Sway borders has some issues with my cursor dissapearing (or is sway HEAD, no idea). Anyways, disabled for now
    # package = pkgs.sway-borders;
    wrapperFeatures.gtk = true;
    config = {
      output = {
        "*" = {
          bg = "\ `find ${../extra/wallpapers/IBM_Developer_Posters.jpg} | shuf -n 1\` fill";
        };
      };
      input = {
        "*".xkb_layout = "us";
        "type:keyboard".xkb_options = "caps:super";
        "type:touchpad".tap = "enabled";
      };
      colors = with config.lib.base16.theme; {
        focused = {
          border = "#${base01-hex}";
          background = "#${base03-hex}";
          text = "#${base03-hex}";
          indicator = "#${base03-hex}";
          childBorder = "#${base03-hex}";
        };
        focusedInactive = {
          border = "#${base02-hex}";
          background = "#${base0A-hex}";
          text = "#${base0A-hex}";
          indicator = "#${base0A-hex}";
          childBorder = "#${base0A-hex}";
        };
        unfocused = {
          border = "#${base01-hex}";
          background = "#${base02-hex}";
          text = "#${base02-hex}";
          indicator = "#${base02-hex}";
          childBorder = "#${base02-hex}";
        };
        urgent = {
          border = "#${base03-hex}";
          background = "#${base0C-hex}";
          text = "#${base0C-hex}";
          indicator = "#${base0C-hex}";
          childBorder = "#${base0C-hex}";
        };
      };
      terminal = "${pkgs.foot}/bin/foot";
      menu = "${pkgs.wofi}/bin/wofi --show drun";
      gaps.inner = 18;
      bars = [];
    };
    extraConfig = ''
      # Remove text on decorations
      for_window [title="."] title_format " "
      default_border normal 0
      default_floating_border normal 0

      for_window [title="."] title_format " "
      default_border normal 0
      default_floating_border normal 0

      # Brightness
      bindsym XF86MonBrightnessDown exec "brightnessctl set 5%-"
      bindsym XF86MonBrightnessUp exec "brightnessctl set +5%"

      # Border Images: needs sway-borders which is finicky at the moment
#     border_images.focused ${../configs/borders/shadows.png}
#     border_images.focused_inactive ${../configs/borders/shadows.png}
#     border_images.unfocused ${../configs/borders/shadows.png}
#     border_images.urgent ${../configs/borders/shadows.png}
    '';
    extraSessionCommands = ''
      XDG_CURRENT_DESKTOP = "sway";
      export WLR_NO_HARDWARE_CURSORS=1
      export SDL_VIDEODRIVER=wayland
      export MOZ_ENABLE_WAYLAND=1
      export _JAVA_AWT_WM_NONREPARENTING=1
    '';
  };
  # foot is a nice wayland native terminal, and I was having openGL issues with alacritty
  programs.foot = {
    enable = true;
    settings = with config.lib.base16.theme; {
      main = {
        font = "Liga SFMono Nerd Font:size=12";
        dpi-aware = "no";
      };
      cursor = {
        style = "beam";
      };
      mouse = {
        hide-when-typing = "yes";
      };
      colors = {
        background = "${base00-hex}";
        foreground = "${base06-hex}";
        regular0 = "${base00-hex}";
        regular1 = "${base0B-hex}";
        regular2 = "${base0C-hex}";
        regular3 = "${base0D-hex}";
        regular4 = "${base07-hex}";
        regular5 = "${base0F-hex}";
        regular6 = "${base09-hex}";
        regular7 = "${base04-hex}";
        bright0 = "${base03-hex}";
        bright1 = "${base0B-hex}";
        bright2 = "${base0C-hex}";
        bright3 = "${base0D-hex}";
        bright4 = "${base07-hex}";
        bright5 = "${base0F-hex}";
        bright6 = "${base09-hex}";
        bright7 = "${base06-hex}";
      };
    };
  };
  # We only want a wayland-native firefox package on wayland, obviously
  programs.firefox.package = pkgs.wrapFirefox pkgs.firefox-unwrapped {
     forceWayland = true;
     extraPolicies = {
       ExtensionSettings = {};
     };
  };
}
#+end_src

** Linux.nix
And for the usual linux config that I want to re-use between systems, here it is!
#+begin_src nix :comments none :tangle "./modules/linux.nix"
{ config, options, pkgs, lib, ... }: {

  # Use nix-unstable
  nix = {
    package = pkgs.nixUnstable;
    extraOptions = ''
      experimental-features = nix-command flakes
    '';
  };

  # Define a user account. Don't forget to set a password with ‘passwd’.
  users.users.shauryasingh = {
    isNormalUser = true;
    extraGroups = [
      "wheel"
      "networkManager"
    ]; # Enable ‘sudo’ for the user + nwcli access
    shell = pkgs.fish;
  };

  # kernel and hardware management
  hardware.enableRedistributableFirmware = true; # fsf :ha:
  boot.kernelPackages =
    pkgs.linuxPackages_latest; # why is default linux so old

  # Bootloader
  boot.loader = {
    efi.canTouchEfiVariables = true;
    systemd-boot.enable = true;
  };

  # Set your time zone.
  time.timeZone = "America/New_York";
  time.hardwareClockInLocalTime =
    true; # lets not mess up my windows clock too

  # Select internationalisation properties.
  i18n.defaultLocale = "en_US.UTF-8";
  console = {
    font = "Lat2-Terminus16";
    keyMap = "us";
  };

  # Sound
  sound.enable = false;
  hardware.pulseaudio.enable = false;

  # Enable pipewire and emulate alsa/pulse/jack
  services.pipewire = {
    enable = true;
    alsa.enable = true;
    alsa.support32Bit = true;
    pulse.enable = true;
    jack.enable = true;
  };

  # Set up XDG for screen-sharing
  xdg = {
    portal = {
      enable = true;
      gtkUsePortal = true;
      extraPortals = with pkgs; [
        xdg-desktop-portal-wlr
        xdg-desktop-portal-gtk
      ];
    };
  };

  # Install fonts
  fonts = {
    fonts = with pkgs; [
      ibm-plex
      emacs-all-the-icons-fonts
      sf-mono-liga-bin
    ];
  };
}
#+end_src

** Theme.nix
Just some basic themeing. Use our flake input (base16-carbon-dark) as our theme systemwide
#+begin_src nix :comments none :tangle "./modules/theme.nix"
{ config, base16-carbon-dark, ...}: {
  config = {
    themes.base16 = {
      enable = true;
      path = "${base16-carbon-dark}/base16-carbon-dark.yaml";
    };
  };
}
#+end_src
